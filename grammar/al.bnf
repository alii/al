<program> ::= { <import> | <whitespace> | <constant_declaration> | <struct_declaration> | <function_declaration> | <comment> }
<whitespace> ::= " " | "\t" | <newline>
<import> ::= "from" <whitespace> <string> <whitespace> "import" <whitespace> <identifier>{ "," [ <whitespace> ] <identifier> } <newline>
<comment> ::= "//" { <char> } <newline>
<constant_declaration> ::= [ <export> <space> ] "const" <whitespace> <identifier> <whitespace> "=" <whitespace> [ "comptime" [<whitespace>] ] [<whitespace>] <expression> [<whitespace>]
<struct_declaration> ::= [ <export> <space> ] "struct" <space> <struct_name> <space> "{" <newline> <struct_member> { ( <newline> <struct_member> ) | <newline> } "}"
<function_declaration> ::= [ <export> <space> ] "fn" <identifier> [ "<" <type_parameter> ">" ] "(" [<parameter_list>] ")" [ ("!" | "?") <type> | <type>] <block>
<type_parameter> ::= <identifier> { "," <identifier> }
<export> ::= "export"
<struct_member> ::= { <space> } <identifier> ":" { <space> } <type> [ { <space> } "=" <space> <expression> ] ","
<parameter_list> ::= <parameter> { "," <parameter> }
<parameter> ::= <identifier> [ <type> ]
<space> ::= " "
<block> ::= "{" { <statement> } "}"
<statement> ::= <declaration> | <assignment> | <function_call> | <condition> | <loop> | <return> | <assert> | <comment>
<declaration> ::= <identifier> [<space>] ":=" [<space>] <expression>
<assignment> ::= <identifier> [<space>] "=" [<space>] <expression>
<function_call> ::= <identifier> [ "." <identifier> ] "(" [ <expression_list> ] ")"
<condition> ::= "if" <expression> <block> [ "else if" <expression> <block> ] [ "else" <block> ]
<loop> ::= "for" [ <identifier> "in" <range_expression> | <expression> ] <block>
<return> ::= "return" [ <space> ] [ <expression> ]
<assert> ::= "assert" <expression> "," <string>
<not> ::= "!"
<expression_list> ::= <expression> { "," <expression> }
<expression> ::= <value> | <identifier> | <function_call> | <error_expression> | <not> <expression> | <binary_operation> | [ "or" ] "{" {<whitespace>} [ statement ] {<whitespace>} <return> [<whitespace>] "}" {<whitespace>}
<value> ::= <number> | <string> | "none" | "true" | "false" | <struct_instance>
<struct_instance> ::= <identifier> "{" <struct_member_assignment> { "," <struct_member_assignment> } "}"
<struct_member_assignment> ::= <identifier> ":" <value>
<error_expression> ::= "error" <string>
<range_expression> ::= <expression> ".." <expression>
<binary_operation> ::= <expression> <operator> <expression>
<operator> ::= "+" | "-" | "*" | "/"
<struct_name> ::= <capitals> { <letter> | <digit> }
<type> ::= <identifier>
<number> ::= <digit> { <digit> } [ "." { <digit> } ]
<string> ::= "'" <chars> "'"
<identifier> ::= <letter> { <letter> | <digit> | "_" } | <identifier> "." <identifier>
<capitals> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<lowers> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<letter>     ::= <lowers> | <capitals>
<digit>      ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<char> ::= <letter> | <digit> | "_" | " " | "." | "," | ":" | ";" | "(" | ")" | "{" | "}" | "[" | "]" | "<" | ">" | "=" | "+" | "-" | "*" | "/" | "!" | "?" | "'" | "\""
<chars> ::= <char> { <char> }
<newline> ::= "\r\n" | "\n"
